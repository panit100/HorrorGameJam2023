BEGIN_OPTIONS
   ScreenPosRequire "LOD_FADE_CROSSFADE || _DITHERCONSTANT || _DITHERFADE"
END_OPTIONS

BEGIN_PROPERTIES
    _DitherAlpha("Dither Alpha", Range(0,1)) = 1
    _DitherAlphaDistance("Dither Alpha Distance", Vector) = (1, 50, 0, 50)
END_PROPERTIES

BEGIN_CBUFFER
    float _DitherAlpha;
    float4 _DitherAlphaDistance;
END_CBUFFER

BEGIN_DEFINES
   #pragma shader_feature_local _ _DITHERCONSTANT _DITHERFADE
   #pragma shader_feature_local _ _DITHERMULTALPHA
   #pragma multi_compile _ LOD_FADE_CROSSFADE
END_DEFINES

BEGIN_CODE

    float Dither8x8Bayer( int x, int y )
    {
        const float dither[ 64 ] = {
                1, 49, 13, 61,  4, 52, 16, 64,
            33, 17, 45, 29, 36, 20, 48, 32,
                9, 57,  5, 53, 12, 60,  8, 56,
            41, 25, 37, 21, 44, 28, 40, 24,
                3, 51, 15, 63,  2, 50, 14, 62,
            35, 19, 47, 31, 34, 18, 46, 30,
            11, 59,  7, 55, 10, 58,  6, 54,
            43, 27, 39, 23, 42, 26, 38, 22};
        int r = y * 8 + x;
        return dither[r] / 64; 
    }

    void ApplyDitherCrossFadeVSP(float2 vpos, float fadeValue)
    {
        if (fadeValue <= 0) clip(-1);
        float dither = Dither8x8Bayer( fmod(vpos.x, 8), fmod(vpos.y, 8) );
        float sgn = fadeValue > 0 ? 1.0f : -1.0f;
        clip(dither - (1-fadeValue) * sgn);
    }

	void SurfaceFunction(inout Surface o, ShaderData d)
	{
		#if (!_HDRP && !_URP && LOD_FADE_CROSSFADE) || (LOD_FADE_CROSSFADE && ((_HDRP || _URP) && UNITY_VERSION < 202220))
            {
                float4 screenPosNorm = d.screenPos / d.screenPos.w;
                screenPosNorm.z = ( UNITY_NEAR_CLIP_VALUE >= 0 ) ? screenPosNorm.z : screenPosNorm.z * 0.5 + 0.5;
                float2 clipScreen = screenPosNorm.xy * _ScreenParams.xy;
                ApplyDitherCrossFadeVSP(clipScreen,unity_LODFade.x);
            }
        #endif

        #if _DITHERFADE
            {
                float4 screenPosNorm = d.screenPos / d.screenPos.w;
                float2 clipScreen = screenPosNorm.xy * _ScreenParams.xy;
                float camDist = distance(d.worldSpacePosition, _WorldSpaceCameraPos);
                float alpha = lerp(_DitherAlphaDistance.y, _DitherAlphaDistance.w, saturate((camDist-_DitherAlphaDistance.x) / max(0.1, _DitherAlphaDistance.z)));
                #if _DITHERMULTALPHA
                    alpha *= o.Alpha;
                #endif
                ApplyDitherCrossFadeVSP(clipScreen, alpha);
            }
        #elif _DITHERCONSTANT
            {
                float4 screenPosNorm = d.screenPos / d.screenPos.w;
                float2 clipScreen = screenPosNorm.xy * _ScreenParams.xy;
                float alpha = _DitherAlpha;
                #if _DITHERMULTALPHA
                    alpha *= o.Alpha;
                #endif
                ApplyDitherCrossFadeVSP(clipScreen, alpha);
            }
        
        #endif


	}

END_CODE


